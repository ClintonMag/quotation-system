REM  *****  BASIC  *****


REM QuotationSystem

REM Read quotation/invoice number from the 'QINUM' sheet
REM Update it for the current quotation/invoice
REM Place new quotation/invoice number in the correct cell
REM Write new quotation/invoice, date, and client name to the
REM 'QINUM.csv' file

' ** IMPORTANT ** '
' The sheet 'Invoice' must immediately follow the sheet 'Quotation'
' The cell containing the last invoice number must be immediately below
' the cell containing the last quotation number.

Option Explicit

' ***** GLOBAL VARIABLES ***** '

'Represent the row and column of any cell, where the cell is represented by a
'1D array with row at index 0 and column at index 1.
'Every (row,col) array name ends in XY
Const ROW As Integer = 0
Const COL As Integer = 1

'Row and Column position of the quotation/invoice number in the sheets where
'they will be used in the quotation/invoice.
'Row 1 in sheet = row 1 in this code
'Col A in sheet = col 1 in this code
Dim QIRecipientXY(2) As Integer
Const QI_REC_ROW As Integer = 7
Const QI_REC_COL As Integer = 6

'Rows and Column of last used quotation and invoice number. invoice number
'must be one row below quotation number
Dim QISourceXY(2) As Integer
Const Q_SRC_ROW As Integer = 1
Const Q_SRC_COL As Integer = 1

'Location in Quotation/Invoice Sheet of the client name
Dim ClientXY(2) As Integer
Const CLIENT_ROW As Integer = 6
Const CLIENT_COL As Integer = 1

'Sheet in which quotations are made
Const QUOTATION_SHEET As String = "Quotation"

'The range of cells within QUOTATION_SHEET that constitute the quotation
Const ACTIVE_RANGE As String = "A1:G1024"

'Sheet in which last used quotation and invoice numbers are stored
'This sheet is hidden and protected
Const QI_NUMBER_SOURCE_SHEET As String = "QINUM"

'Number of digits in a quotation/invoice number
Const QINUM_LENGTH = 7

'The name of the ods document for which this macro is meant
Const PARENT_FILE As String = "Stock_And_Quotes.ods"

'The csv file where list of quotation numbers used will be stored
'It's a csv for now, database to be used in future
Const CSVFILE = "QINUM.csv"

'The folder that will contain new quotations/invoices
Const NEW_QUOTATIONS_AND_INVOICES_FOLDER As String = "New_Quotations_And_Invoices/"

'The current spreadsheet document
Dim Doc As Object

'All sheets contained by the current spreadsheet document
Dim AllSheets As Object

'The applicable working sheet, either 'Quotation' or 'Invoice'
Dim QISheet As Object

'Store the full quotation number as a string
Dim QIString As String

'New quotation number as a string
Dim NewQIString As String

'Index of the sheet named 'Quotation' in the ThisComponent.Sheets list
Dim QSheetIndex as Integer


' ***** FUNCTIONS ***** '

Function Cells(sheet As Object, x As Integer, y As Integer) As Object
	REM Simplify calls to retrieve cell position by coordinates
	REM x and y are cell indices that start at 1. So cell A1 is cell (1,1).
	
	Cells = sheet.getCellByPosition(y-1, x-1)
End Function


' ***** SUBROUTINES ***** '
	
Sub MainQuotation
	REM Entry point if working on a quotation
	
	'Indicate whether we are working on a quotation or an invoice
	'0 for quotation, 1 for invoice
	Dim QuoteOrInv as Integer
	
	QuoteOrInv = 0
	MainProcess(QuoteOrInv)

End Sub

Sub MainInvoice
	REM Entry point if working on an invoice

	'Indicate whether we are working on a quotation or an invoice
	'0 for quotation, 1 for invoice
	Dim QuoteOrInv as Integer
	
	QuoteOrInv = 1
	MainProcess(QuoteOrInv)

End Sub


Sub MainProcess(QuoteOrInv)
	REM Start the main invoice/quotation nunmber update procedure once it's
	REM which is to be updated
	
	
	' ***** Check if user is done working on a quotation/invoice ***** '
	
	
	'String to display in message box to check completion of quotation/invoice
	Dim Query As String
	'Title of message box to appear in title bar
	Dim Title As String
	'The word 'quotation' or 'invoice', depending on which one is worked on
	Dim DocumentType As String
	
	If QuoteOrInv = 0 Then
		DocumentType = "Quotation"
	Else
		DocumentType = "Invoice"
	End If
	
	Title = DocumentType & " Complete?"
	
	Query = "Did you complete the entire " & DocumentType & _
			" and did you also enter the Customer Name?"
	
	'Check if the quotation/invoice has been completed
	If MsgBox(Query, _
			  MB_YESNO + MB_DEFBUTTON2 + MB_ICONQUESTION, _
			  Title) = IDNO Then
		Exit Sub
	End If
	
	
	' ***** Initializes the global variables ***** ' 
	
	Doc = ThisComponent
	
	AllSheets = Doc.Sheets
	
	QSheetIndex = AllSheets.getByName(QUOTATION_SHEET).RangeAddress.Sheet
	
	' If working on quotation, QSheetIndex + QoutOrInv will be the "Quotation"
	' sheet. If working on invoice,vQSheetIndex + QoutOrInv will be the
	' "Invoice" sheet
	QISheet = AllSheets.getByIndex(QSheetIndex + QuoteOrInv)
	
	'Saving row-col values of 3 cells in their respective arrays
	
	QIRecipientXY(ROW) = QI_REC_ROW
	QIRecipientXY(COL) = QI_REC_COL
	
	QISourceXY(ROW) = Q_SRC_ROW
	QISourceXY(COL) = Q_SRC_COL
	
	ClientXY(ROW) = CLIENT_ROW
	ClientXY(COL) = CLIENT_COL
	
	QIString = Cells(AllSheets.getByName(QI_NUMBER_SOURCE_SHEET), _
					 QISourceXY(ROW) + QuoteOrInv, QISourceXY(COL)).String
					 
	'Select any cell (here, it's cell A1) to force any cells in edit mode to
	'to commit their contents. Required so that the new client name can be
	'stored in the cell if the button was pressed immediately after the client
	'name was typed out, but before the Enter button was pressed.
	Doc.CurrentController.select(Cells(QISheet,1,1))


	' *** Generate a new quotation/invoice number, place it where needed. *** '
	
	'Read the last quotation/invoice number, increase it by 1 and place it
	'in the correct cell
	
	Dim i as Integer
	
	'String format to be used by the Format function to specify number of zeros
	'for left-padding
	Dim QINumFormatString As String
	
	'Quotation/Invoice Number
	Dim QINum As Long
	
	'New Quotation/Invoice Number
	Dim NewQINum as Long
	
	'Cell where last used quotation/invoice number is stored
	Dim QISourceCell As Object
	
	'Cell to receive the new quotation or invoice number in the QUOTATION_SHEET
	'or the QI_NUMBER_SOURCE_SHEET
	Dim QICell as Object

	'Extract quotation/invoice integer portion, increment by 1.
	QINum = Val(Right(QIString, QINUM_LENGTH))
	NewQINum = QINum + 1
	
	QISourceCell = Cells(AllSheets.getByName(QI_NUMBER_SOURCE_SHEET), _
						 QISourceXY(ROW) + QuoteOrInv, QISourceXY(COL))
	
	QICell = Cells(QISheet, QIRecipientXY(ROW), QIRecipientXY(COL))
	
	QINumFormatString = "0"
	'Compute format string. E.g. if quotation number is 7 digits, format string
	'will be 7 zeros: 0000000
	For i = 1 to QINUM_LENGTH-1
		QINumFormatString = QINumFormatString & "0"
	Next i
	
	If QuoteOrInv = 0 Then
		NewQIString = "Q"
	Else
		NewQIString = "I"
	End If
	
	NewQIString = NewQIString & Format(NewQINum, QINumFormatString)
	'Place new value of quotation/invoice number in QICell
	QICell.String = NewQIString
	'Place new value of quotation/invoice number in QI_NUMBER_SOURCE_SHEET
	QISourceCell.String = NewQIString
	
	
	' ***** Write this new quotation/invoice number to a csv file ***** '
	
	'Write an entry into CSVFILE for each new quotation or invoice written.
	'Format of one row of CSVFILE:
	'Quotation/Invoice Number,QuoteOrInv Boolean value, Date, Name of client

	'The string to be written to the QINUM.csv file
	Dim DataEntry As String
	'Client Name
	Dim Client As String
	'The path of PARENT_FILE, the document this macro is for.
	Dim DocPath As String
	'Index of first appearance of parent file name in it's directory string 
	'E.g. If file is "file:///the/myfile.ods", the index of the 'm' in the
	'file name "myfile.ods" will be 13 (they start at 1).
	Dim FileNameIndex As Integer
	'File path of the csv file in url format
	Dim CSVFileURL As String
	'The file handle through which communication with the CSVFILE is done
	Dim FileNo As Integer
	'Directory to the folder in which PARENT_FILE is contained
	Dim ParentDir As String
	
	
	Client = Cells(QISheet, ClientXY(ROW), ClientXY(COL)).String
	' The keyword 'Date' used in this context refers to today's date
	DataEntry = NewQIString & "," & QuoteOrInv & "," & Date & "," & Client
	
	DocPath = Doc.getURL()
	FileNameIndex = InStr(DocPath, PARENT_FILE)
	ParentDir = Mid(DocPath, 1, FileNameIndex-1)
	CSVFileURL = ParentDir & CSVFILE
	
	'The DataEntry string will be written to the file specified by CSVFileURL
	
	'FreeFile function is used to get a free file handle	
	FileNo = FreeFile
	'Indentation used as file is free for use between Open and Close statements
	Open CSVFileURL For Append As #FileNo
		
		Print #FileNo, DataEntry
	
	Close #FileNo
	
	
	' ***** Save file to keep last used quotation/invoice number ***** '
	
	'Save the file
	Doc.store
	
	'Remove the button used to run this very script
	'Unprotect sheet; can't delete controls from protected sheets
	QISheet.unprotect("")
	
	Dim ActiveRange As Object
	ActiveRange = QISheet.getCellRangeByName(ACTIVE_RANGE)
	ActiveRange.clearContents(com.sun.star.sheet.CellFlags.OBJECTS)
	'Protect again
	QISheet.protect("")
	
	'Make a copy of file containing only the quotation/invoice worked on
	
	'Remove all sheets that aren't QISheet
	i = 0
	Dim TmpSheet As Object
	Do While AllSheets.Count > 1
		TmpSheet = AllSheets.getByIndex(i)
		If TmpSheet.Name <> QISheet.Name Then
			AllSheets.removeByName(TmpSheet.Name)
		Else
			i = i + 1
		End If
	Loop
	
	'Full path to the copy. The copy will be named by its quotation/invoice no.
	Dim NewDocPath As String
	NewDocPath = ParentDir & NEW_QUOTATIONS_AND_INVOICES_FOLDER _
				 & NewQIString & ".ods"

	Dim Props()
	
	Doc.storeToURL(NewDocPath, Props())
	
	'Open the copy
	StarDesktop.loadComponentFromURL(NewDocPath, "_blank", 0, Props())
	
	'Close the main document to prevent saving a sheet with deleted sheets
	Doc.close(True)
	
End Sub


Sub Test
	'A subroutine to experiment with code to study it's behaviour.
	'Frequently replaced with new test code.

	Doc = ThisComponent
	AllSheets = Doc.Sheets
	
	Dim Doc1 As Object, Doc2 As Object, obj1 As Object, obj2 As Object, obj3 As Object
	Dim MySheet1 As Object, MySheet2 As Object
	Dim MyTable As Object
	Dim MyRange1 As Object, MyRange2 As Object
	Dim mystr1 As Boolean, mystr2 As Boolean, mystr3 As Boolean
	
	Dim NewDoc As Object
	Dim NewDocPath As String
	NewDocPath = "file:///C:testdoc.ods"
	
	'Properties of NewDoc
	'Open NewDoc in hidden mode by setting up it's properties
'	Dim Props(0) As New com.sun.star.beans.PropertyValue
'	Props(0).name = "Hidden"
'	Props(0).value = "True"
	Dim Props()	
	'Template used for NewDoc. Default template to be used.
	Dim NewDocTemplate As String
	NewDocTemplate = "private:factory/scalc"
	
	NewDoc = StarDesktop.loadComponentFromURL(NewDocTemplate, "_blank", 0, Props())
	
	MySheet1 = AllSheets.getByName("Quotation")
	MyRange1 = MySheet1.getCellRangeByName("A1:G1024")	
	obj1 = NewDoc.Sheets.getByIndex(0)
	obj1.Name = "Q123"
	obj3 = obj1.getCellRangeByName("A1:G1024")

	obj3.DataArray = MyRange1.DataArray
	
End Sub
